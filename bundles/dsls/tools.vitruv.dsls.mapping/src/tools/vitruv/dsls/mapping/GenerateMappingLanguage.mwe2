module tools.vitruv.dsls.mapping.GenerateMapping

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import tools.vitruv.dsls.mapping.postprocessor.MappingLanguageExtendedGenerator

var rootPath = ".."
var projectName = "tools.vitruv.dsls.mapping"
var testProjectPath = "${rootPath}/../../tests/dsls/${projectName}"

Workflow {
	component = MappingLanguageExtendedGenerator {//XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "tools.vitruv.dsls.mapping"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "${testProjectPath}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${testProjectPath}.ui.tests"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		cleaner = {
			enabled = false
		}
		language = StandardLanguage {
			name = "tools.vitruv.dsls.mapping.MappingLanguage"
			fileExtensions = "mapping"
			junitSupport = junit.Junit4Fragment2 {
				generateStub = false
			}
			referencedResource = "platform:/resource/tools.vitruv.dsls.mirbase/model/generated/MirBase.genmodel"
		}
	}
}